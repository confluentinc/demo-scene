buildscript {
  ext {
    springBootVersion = '2.2.0.M2'
    shadowPluginVersion = '5.0.0'
    kotlinVersion = '1.3.21'
    jibPluginVersion = '1.3.0'
    confluentVersion = '5.2.1'
    kafkaVersion = '2.2.0'
    lombokVersion = '1.18.8'
  }
  repositories {
    jcenter()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://kotlin.bintray.com/kotlinx" }
  }

  dependencies {
    classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.14.2"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "com.github.jengelman.gradle.plugins:shadow:${shadowPluginVersion}"
    classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:${jibPluginVersion}"

    // kotlin compiler
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    classpath "org.jetbrains.kotlin:kotlin-serialization:${kotlinVersion}"
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'

  sourceCompatibility = 1.8

  repositories {
    jcenter()
    maven { url 'http://packages.confluent.io/maven/' }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url 'https://jitpack.io' }
  }
}

configure(subprojects.findAll { (it.name == "streams") }) {
  apply plugin: 'application'
}

project(':core') {
  apply plugin: "com.commercehub.gradle.plugin.avro"

  avro {
    // stringType = "CharSequence"
  }

  dependencies {
    compile 'org.apache.avro:avro:1.8.2'
    compile "org.apache.kafka:kafka-clients:${kafkaVersion}"
    compile 'javax.json:javax.json-api:1.1.4'
    runtime 'org.glassfish:javax.json:1.1.4'
    compile "io.confluent:kafka-streams-avro-serde:${confluentVersion}"
    compile("com.github.gAmUssA:streaming-utils:-SNAPSHOT") {
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
  }

}

project(':loader') {
  apply plugin: 'groovy'
  dependencies {
    compile project(':core')
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
  }

  def env = System.getenv()

  ['AvroRatingStreamer', 'JSONRatingStreamer', 'RawRatingStreamer'].each { generatorName ->
    tasks.create(name: "streamWith${generatorName}", type: JavaExec) {
      main = generatorName
      classpath = sourceSets.main.runtimeClasspath
      args = [getConfigPath()]
    }
  }

  task loadAvroMovies(type: JavaExec) {
    main = 'AvroMovieLoader'
    classpath = sourceSets.main.runtimeClasspath
    args = [getConfigPath()]
  }

  jar {
    manifest {
      attributes "Main-Class": ""
    }

    from {
      configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
  }

}

project(':spring-web-loader') {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  dependencies {
    compile project(':streams')

    compile "org.springframework.boot:spring-boot-starter-web"
    compile 'org.springframework.kafka:spring-kafka'
  }

  configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  }
}

project(':streams') {
  mainClassName = "io.confluent.demo.StreamsDemo"

  dependencies {
    compile project(':core')

    compile "org.slf4j:slf4j-log4j12:1.7.25"
    compile "org.apache.kafka:kafka-streams:${kafkaVersion}"
    compile "io.confluent:monitoring-interceptors:${confluentVersion}"

//  compile 'io.confluent:kafka-avro-serializer:3.3.0'
//  compile 'io.confluent:kafka-schema-registry-client:3.3.0'
    testCompile 'junit:junit:4.12'
    testCompile "org.apache.kafka:kafka-streams-test-utils:${kafkaVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  }

  task runApp(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = mainClass
    def configPath = project.getProperty('configPath')
    args = [configPath]
  }
}

project(':spring-streams') {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  bootJar {
    mainClassName = "io.confluent.demo.SpringStreamsDemo"
  }

  dependencies {
    compile project(':streams')

    compile "org.springframework.boot:spring-boot-starter-web"
    compile 'org.springframework.kafka:spring-kafka'
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile 'junit:junit'
  }

  configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  }

  dependencyManagement {
    imports {
//      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
      mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
  }
}

def getConfigPath() {
  new File(project.getProperty('configPath')).absolutePath
}

/**
 * Project to build docker image for Kubernetes + deployment scripts
 */
project(':streams-kubernetes') {
  apply plugin: 'com.github.johnrengelman.shadow'
  apply plugin: 'com.google.cloud.tools.jib'

  def imageBaseName = "streaming-movie-ratings"

  /**
   * Build an image based on «streams» project
   */
  dependencies {
    compile project(':streams')
  }

  // tag::jibConfig[]
  jib {
    to {
      image = "gamov-docker.jfrog.io/dev/${imageBaseName}"  // <1>
      //image = "gcr.io/cloud-private-dev/gamussa/${imageBaseName}" // <2>
      //credHelper = 'gcr' <3>
      // end::jibConfig[]
      from {
        image = "gcr.io/distroless/java:8"
        //image = "gcr.io/distroless/java:debug"
        //image = "bellsoft/liberica-openjdk-alpine:8u202"
        //image = "bellsoft/liberica-openjre-debian:8u202"
      }

      container {
        mainClass = project(":streams").mainClass
        args = [
            '/config'
        ]
      }
    }

    // TODO: configure via environment variables/configMaps in Kubernetes
    extraDirectory = file("${System.properties['user.home']}/.ccloud")
  }
}

project(':streams-kotlin') {
  apply plugin: 'kotlin'
  apply plugin: 'kotlinx-serialization'

  dependencies {
    compile project(':streams')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile "org.slf4j:slf4j-log4j12:1.7.25"
  }
}
