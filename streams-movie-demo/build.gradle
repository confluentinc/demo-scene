buildscript {
  ext {
    springBootVersion = '2.1.0.M3'
  }
  repositories {
    jcenter()
    maven { url "https://repo.spring.io/milestone" }
  }
  dependencies {
    classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.14.2"
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'

  sourceCompatibility = 1.8

  repositories {
    jcenter()
    maven {
      url 'http://packages.confluent.io/maven/'
    }
    maven { url "https://repo.spring.io/milestone" }
  }
}

configure(subprojects.findAll { (it.name == "streams") }) {
  apply plugin: 'application'
}

project(':core') {

  apply plugin: "com.commercehub.gradle.plugin.avro"

  avro {
    stringType = "CharSequence"
  }

  dependencies {
    compile 'org.apache.avro:avro:1.8.2'
    compile 'org.apache.kafka:kafka-clients:2.0.0'
    compile 'javax.json:javax.json-api:1.1.2'
    runtime 'org.glassfish:javax.json:1.1.2'
    compile 'io.confluent:kafka-streams-avro-serde:5.0.0'
  }

}

project(':loader') {
  apply plugin: 'groovy'
  dependencies {
    compile project(':core')
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
  }

  def env = System.getenv()

  ['AvroRatingStreamer', 'JSONRatingStreamer', 'RawRatingStreamer'].each { generatorName ->
    tasks.create(name: "streamWith${generatorName}", type: JavaExec) {
      main = generatorName
      classpath = sourceSets.main.runtimeClasspath
      args = [project.getProperty('configPath')]
    }
  }

  task loadAvroMovies(type: JavaExec) {
    main = 'AvroMovieLoader'
    classpath = sourceSets.main.runtimeClasspath
    args = [project.getProperty('configPath')]
  }

  jar {
    manifest {
      attributes "Main-Class": ""
    }

    from {
      configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
  }

}

project(':spring-web-loader') {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  dependencies {
    compile project(':core')

    compile "org.springframework.boot:spring-boot-starter-web"
    compile 'org.springframework.kafka:spring-kafka'
  }

  configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  }
}

project(':streams') {
  mainClassName = "io.confluent.demo.StreamsDemo"

  dependencies {
    compile project(':core')

    compile 'org.apache.kafka:kafka-streams:2.0.0'
    compile 'io.confluent:monitoring-interceptors:5.0.0'

//  compile 'io.confluent:kafka-avro-serializer:3.3.0'
//  compile 'io.confluent:kafka-schema-registry-client:3.3.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.kafka:kafka-streams-test-utils:2.0.0'
    testCompile 'org.projectlombok:lombok:1.16.20'
  }

  task runApp(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = mainClass
    def configPath = project.getProperty('configPath')
    args = [configPath]
  }

}

project(':spring-streams') {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  bootJar {
    mainClassName = "io.confluent.demo.SpringStreamsDemo"
  }

  dependencies {
    compile project(':streams')

    compile "org.springframework.boot:spring-boot-starter-web"
    compile 'org.springframework.kafka:spring-kafka'
  }
  // Greenwich.M1 due Sept 21; use boot for dep management instead
  ext {
    springCloudVersion = 'Finchley.RC2'
  }

  configurations.all {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  }

  dependencyManagement {
    imports {
//      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
      mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
  }
}
