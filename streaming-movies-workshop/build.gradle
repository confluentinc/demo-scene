buildscript {
  ext {
    springBootVersion = '2.4.1'
  }
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

allprojects {
  group = 'io.confluent.developer'
  version = '0.0.1-SNAPSHOT'
}

configure(subprojects.findAll { !it.name.startsWith("core") }) {

  apply plugin: 'java'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  repositories {
    jcenter()
    maven {
      url 'https://packages.confluent.io/maven'
    }
  }

  sourceCompatibility = '11'

  tasks.withType(JavaCompile) {
    options.compilerArgs = [
        '-Xlint:deprecation',
        '-Werror'
    ]
  }

  tasks.withType(Test) {
    testLogging {
      outputs.upToDateWhen { false }
      events 'standard_out'
      exceptionFormat = "full"
    }
  }
}



